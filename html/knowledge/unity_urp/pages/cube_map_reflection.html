<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta http-equiv="X-UA-Compatible" content="IE=edge">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>Document</title>
</head>
<body>
<xmp>
![image](../res/cube_map_reflection.jpg)

```c++
Shader "URP/CubeMapReflection" {

	Properties {
		_Color ("Color Tint", Color) = (1, 1, 1, 1)
		_ReflectColor ("Reflection Color", Color) = (1, 1, 1, 1)
		_ReflectAmount ("Reflect Amount", Range(0, 1)) = 1
		_Cubemap ("Reflection Cubemap", Cube) = "_Skybox" {}
	}

	SubShader {

		Tags { "RenderType"="Opaque" "Queue"="Geometry"}
		
		Pass { 
			
			HLSLPROGRAM
			
			#pragma multi_compile_fwdbase
			
			#pragma vertex vert
			#pragma fragment frag

            #include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Core.hlsl"
            #include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Lighting.hlsl"
			#include "Packages/com.unity.render-pipelines.universal/Shaders/2D/Include/LightingUtility.hlsl"

            TEXTURECUBE(_Cubemap);
            SAMPLER(sampler_Cubemap);

            CBUFFER_START(UnityPerMaterial)
			half4 _Color;
			half4 _ReflectColor;
			half _ReflectAmount;
            CBUFFER_END
			
            struct Attributes
            {
                float4 positionOS               : POSITION;
                float3 normalOS                 : NORMAL;
                float4 tangentOS                : TANGENT;
                UNITY_VERTEX_INPUT_INSTANCE_ID
            };

            struct Varyings 
            {
                float4 positionCS               : SV_POSITION;
                float3 normalWS                 : TEXCOORD0;
				float3 refDirWS                 : TEXCOORD1;
                UNITY_VERTEX_INPUT_INSTANCE_ID
            };

            Varyings vert(Attributes input)
            {
                Varyings output = (Varyings)0;
                UNITY_SETUP_INSTANCE_ID(input);
                UNITY_TRANSFER_INSTANCE_ID(input, output);

                VertexPositionInputs vertexInput = GetVertexPositionInputs(input.positionOS.xyz);
                VertexNormalInputs normalInput = GetVertexNormalInputs(input.normalOS, input.tangentOS);

                output.positionCS = vertexInput.positionCS;
                output.normalWS = normalInput.normalWS;
                float3 viewDirWS = GetWorldSpaceViewDir(vertexInput.positionWS);
				output.refDirWS = reflect(-viewDirWS, output.normalWS);

                return output;
            }

			half4 frag(Varyings input) : SV_Target {

                UNITY_SETUP_INSTANCE_ID(input);

				float3 normalWS = normalize(input.normalWS);
                float3 lightDirWS = normalize(_MainLightPosition.xyz);
				float3 refDirWS = normalize(input.refDirWS);		
				
				half3 ambient = unity_AmbientSky.xyz;
				
				half3 diffuse = _MainLightColor.rgb * _Color.rgb * max(0, dot(normalWS, lightDirWS));
				
				half3 reflection = SAMPLE_TEXTURECUBE_LOD(_Cubemap, sampler_Cubemap, refDirWS, 1) * _ReflectColor.rgb;
								
				half3 color = ambient + lerp(diffuse, reflection, _ReflectAmount);
				
				return half4(color, 1.0);
			}
			
			ENDHLSL
		}
	}
}

```
</xmp>
</body>
</html>